package main

func blinkStatusOn () (out bool) {
  var t f32 = f64.f32(glfw.GetTime())
  t = getValueUnderMax(t, 0.15)
  
  if t < 0.075 {
    out = true
  }
}

func getTimeBasedOffset () (out f32) { // in the range of 0.0 to 0.5
  out = f64.f32(glfw.GetTime())
  out = getValueUnderMax(out, 0.5)
  
  if out > 0.25 {
    out = f32.sub(0.25, out) // get oscillating range from -0.25 to +0.25
  }
}

func getValueUnderMax(in f32, max f32) (out f32) {
  for in >= max {
    in = f32.sub(in, max)
  }
  
  out = in
}

func isTimerOver(timer f64, limit f64) (is_over bool) {
  if gteq(f64.sub(glfw.GetTime(), timer), limit) {
    is_over = true
  }
}

func getLerpTween(a Position2D, b Position2D, timer f64, limit f64) (out Position2D) {
  var xDelta f32 = b.x - a.x
  var yDelta f32 = b.y - a.y
  var timeDelta f64 = glfw.GetTime() - timer
  var frac f32 = f64.f32(timeDelta / limit)
  out.x = f32.add(a.x, f32.mul(frac, xDelta))
  out.y = f32.add(a.y, f32.mul(frac, yDelta))
}



// Collision manager functions

func checkPlayerGhostCollision(ghost Ghost) () {
  if bool.and(i32.eq(player.index_x, ghost.index_x), i32.eq(player.index_y, ghost.index_y)) {
    if i32.eq(ghost.status, GHOST_MODE_FRIGHTENED) {
      ghosts[ghost.gtype].index_x = 14
      ghosts[ghost.gtype].index_y = 13
      ghosts[ghost.gtype].status = GHOST_MODE_IN_HOUSE
    }

    if bool.or(i32.eq(ghost.status, GHOST_MODE_CHASE), i32.eq(ghost.status, GHOST_MODE_SCATTER)) {
      PLAYER_LIVES = i32.sub(PLAYER_LIVES, 1)
      nextLifePos = coords2position(LIFE_SPAWN_LINE, i32.sub(LIFE_SPAWN_COLUMN, PLAYER_LIVES - 1))
      GETREADY_TIMER = glfw.GetTime()
      gameMode = GAMEMODE_GETREADY
      
      if i32.lteq(PLAYER_LIVES, 0) {
        gameMode = GAMEMODE_GAMEOVER
        str.print("------ GAME OVER ------") // FIX ME add game over screen
      } else {
        player = newPlayer(PLAYER_SPAWN_LINE, PLAYER_SPAWN_COLUMN, PLAYER_RADIUS)
      }
    }
  }
}
